import com.fasterxml.jackson.databind.ObjectMapper

import java.time.Instant

plugins {
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java'
    id 'com.github.ben-manes.versions' version '0.44.0'
    id 'net.researchgate.release' version '3.0.2'
}

group = 'com.ruchij'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    compileOnly 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.springframework.session:spring-session-jdbc'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

task information {
    doLast {
        def file = new File("$buildDir/resources/main/information.json")
        file.parentFile.mkdirs()

        if (file.exists()) {
            file.delete()
        }

        def serviceInformation =
                Map.of(
                        "group", project.group,
                        "name", project.name,
                        "version", project.version,
                        "gradleVersion", gradle.gradleVersion,
                        "buildTimestamp", Instant.now().toString(),
                        "gitBranch", "git rev-parse --abbrev-ref HEAD".execute().text.trim(),
                        "gitCommit", "git rev-parse --short HEAD".execute().text.trim()
                )

        file.withWriter {
            writer ->
                new ObjectMapper().writeValue(writer, serviceInformation)
        }
    }
}

classes {
    dependsOn information
}

release {
    preTagCommitMessage = 'Setting release version: '
    newVersionCommitMessage = 'Setting development version: '
    pushReleaseVersionBranch = "main"

    git {
        requireBranch.set('dev')
    }
}

bootJar {
    archiveName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}
